/*
 * Copyright (C) 2022 Uwais Alqadri
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'org.jetbrains.kotlin.jvm' version "1.6.10"
  id 'org.jetbrains.kotlin.kapt' version "1.6.10"
  id 'com.google.devtools.ksp' version "1.6.10-1.0.2"
  id 'application'
  id 'com.diffplug.spotless' version "6.1.0"
  id 'com.github.ben-manes.versions' version "0.39.0"
  id 'org.jetbrains.kotlin.plugin.serialization' version "1.6.10"
}

repositories {
  mavenCentral()
}

def compileArgs = ['-Xjsr305=strict', '-progressive', '-Xopt-in=kotlin.ExperimentalStdlibApi']

compileKotlin {
  kotlinOptions {
    jvmTarget = "11"
    freeCompilerArgs += compileArgs
  }
}

compileTestKotlin {
  kotlinOptions {
    jvmTarget = "11"
    freeCompilerArgs += compileArgs
  }
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(11))
  }
}

sourceSets {
  main {
    java {
      srcDir("build/generated/source/kapt/main")
    }
  }
}

mainClassName = 'com.uwaisalqadri.UpdateReadmeKt'

// Fat jar configuration to run this as a standalone jar
// Configuration borrowed from https://stackoverflow.com/a/49284432/3323598
jar {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  manifest {
    attributes(
        'Main-Class': 'com.uwaisalqadri.UpdateReadmeKt'
    )
  }
  from {
    configurations.compileClasspath.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

dependencies {
  implementation "com.github.ajalt:clikt:2.8.0"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
  implementation("io.ktor:ktor-client-core:2.0.0")
  implementation("io.ktor:ktor-serialization-kotlinx-xml:2.0.0")
  implementation("io.ktor:ktor-serialization-kotlinx-json:2.0.0")
  implementation("io.ktor:ktor-client-content-negotiation:2.0.0")
  implementation("io.ktor:ktor-client-logging:2.0.0")
}
